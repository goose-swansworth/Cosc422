#version 400

layout(quads, equal_spacing, ccw) in;

uniform mat4 mvpMatrix;
uniform sampler2D heightMap;
uniform int waveTick;
uniform float waterHeight;

out float worldHeight;
out vec3 worldPosition;
out vec2 texCoords;
out vec3 N;

vec4 sampleHeightMap(vec4 position) {
    float xMin = -45, xMax = +45, zMin = 0, zMax = -90;
    vec2 heightMapTexCoords = vec2(
        (position.x - xMin) / (xMax - xMin),
        (position.z - zMin) / (zMax - zMin)
    );
    float height = texture(heightMap, heightMapTexCoords).r;
    position.y = height * 10.0;

    float aMin = 0.05;
    float aMax = 0.15;
    float omega = 8; 
    float delta = 0.005;

    float d = (waterHeight - position.y) / waterHeight;

    if (position.y <= waterHeight) {
        position.y = waterHeight + mix(aMin, aMin, d) * sin(omega*(d + delta * waveTick));
    }
    return position;
}


void main() {
    
    vec4 vert;
    float epsilon = 0.001;

    float u = gl_TessCoord.x;
    float v = gl_TessCoord.y;
    texCoords = vec2(u, v);

    vec4 p0, p1, p2, p3;
    p0 = gl_in[0].gl_Position;
    p1 = gl_in[1].gl_Position;
    p2 = gl_in[2].gl_Position;
    p3 = gl_in[3].gl_Position;

    vert = (1 - u)*(1 - v) * p0 + u*(1-v) * p1 + u*v * p2 + (1 - u)*v * p3;         
    vert = sampleHeightMap(vert);
    worldPosition = vec3(vert);

    float yN = sampleHeightMap(vert - vec4(0, 0, epsilon, 0)).y;
    float yE = sampleHeightMap(vert + vec4(epsilon, 0, 0, 0)).y;
    float yS = sampleHeightMap(vert + vec4(0, 0, epsilon, 0)).y;
    float yW = sampleHeightMap(vert - vec4(epsilon, 0, 0, 0)).y;

    N = normalize(vec3(2*(yW - yE)*epsilon, 4*epsilon*epsilon, 2*(yN - yS)*epsilon));



    

    gl_Position = mvpMatrix * vert;
}
